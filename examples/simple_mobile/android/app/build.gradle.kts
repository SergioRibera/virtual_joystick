plugins {
    alias(libs.plugins.android.application)
}

kotlin {
    compilerOptions {
        languageVersion = org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_2_3
        jvmToolchain(8)
    }
}

android {
    namespace = "com.sergioribera.simple"
    compileSdk = 36

    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/DefaultConfig
    defaultConfig {
        applicationId = "com.sergioribera.simple"
        minSdk = 31
        targetSdk = 36
        // NOTE: Increase by 1 on each release
        versionCode = 1
        // NOTE: Update with full semantic version on each release
        versionName = "0.1.0"
        // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/variant/ExternalNativeBuild
        // NOTE: We need this, otherwise libc++_shared.so will not be inserted
        @Suppress("UnstableApiUsage")
        externalNativeBuild {
            cmake {
                arguments("-DANDROID_STL=c++_shared")
            }
        }
        // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/Ndk
        ndk {
            abiFilters.addAll(listOf("arm64-v8a", "armeabi-v7a", "x86_64"))
        }
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/ExternalNativeBuild
    externalNativeBuild {
        cmake {
            path = file("CMakeLists.txt")
        }
    }
    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/BuildType
    buildTypes {
        getByName("release") {
            // https://developer.android.com/topic/performance/app-optimization/enable-app-optimization
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"))
        }
    }
    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/CompileOptions
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/BuildFeatures
    buildFeatures {
        prefab = true
    }
    packaging {
        // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/JniLibsPackaging
        jniLibs.excludes.add("lib/*/libdummy.so")
        jniLibs.pickFirsts.add("lib/*/libc++_shared.so")
    }
    // https://developer.android.com/reference/tools/gradle-api/8.13/com/android/build/api/dsl/AndroidSourceSet
    sourceSets {
        getByName("main") {
            assets {
                directories += "../../assets"
            }
        }
    }
}

dependencies {
    implementation(libs.appcompat)
    implementation(libs.core)
    implementation(libs.material)
    implementation(libs.games.activity)
    implementation(libs.core.ktx)
}
